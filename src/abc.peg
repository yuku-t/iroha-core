{
  var AbcFile              = require("./AbcFile").AbcFile
    , AbcTune              = require("./AbcTune").AbcTune
    , ComposerField        = require("./ComposerField").ComposerField
    , KeyField             = require("./KeyField").KeyField
    , MeterField           = require("./MeterField").MeterField
    , ReferenceNumberField = require("./ReferenceNumberField").ReferenceNumberField
    , TitleField           = require("./TitleField").TitleField
    , TuneBody             = require("./TuneBody").TuneBody
    , TuneHeader           = require("./TuneHeader").TuneHeader
    , TuneLine             = require("./TuneLine").TuneLine
}

Start
  = AbcFile

AbcFile
  = abcTune:AbcTune { return new AbcFile(abcTune); }

AbcTune
  = tuneHeader:TuneHeader tuneBody:TuneBody { return new AbcTune(tuneHeader, tuneBody); }

TuneHeader
  = xf:ReferenceNumberField tf:TitleField ifs:InformationField* kf:KeyField {
    return new TuneHeader(xf, tf, kf, ifs);
  }

InformationField
  = ComposerField
  / MeterField

ComposerField
  = "C:" text:TextString Linefeed { return new ComposerField(text); }

KeyField
  = "K:" key:Key Linefeed { return new KeyField(key); }

MeterField
  = "M:" text:TextString Linefeed { return new MeterField(text); }

TitleField
  = "T:" text:TextString Linefeed { return new TitleField(text); }

ReferenceNumberField
  = "X:" number:Number Linefeed { return new ReferenceNumberField(number); }

Key
  = KeyNote KeyAccidental? ModeSpec?

KeyNote
  = [A-G]

KeyAccidental
  = "#"
  / "b"

ModeSpec
  = Space* mode:Mode { return mode; }

Mode
  = ModeMajor { return "major"; }
  / ModeMinor { return "minor"; }

ModeMajor
  = [m]i [a]i [j]i ([o]i [r]i)?

ModeMinor
  = [m]i ([i]i [n]i [o]i [r]i)?

TuneBody
  = tuneLines:TuneLine+ { return new TuneBody(tuneLines); }

TuneLine
  = Element+ Linefeed? { return new TuneLine(); }

Element
  = NotesGroup
  / BarLine
  / Space

NotesGroup
  = NoteElement+

NoteElement
  = Note NoteLength?

Note
  = [a-gA-G]

NoteLength
  = Number? ("/"+ Number?)?

BarLine
  = "|:"
  / ":|"
  / "|"

Linefeed
  = "\r\n" / "\n"

Space
  = [ \t]

TextString
  = cs:TextChar* { return cs.join(""); }

TextChar
  = [^\n]

Number
  = d:$[0-9]+ { return parseInt(d, 10); }
